// Copyright (c) 2026 WSO2 LLC. (https://www.wso2.com).
//
// WSO2 LLC. licenses this file to you under the Apache License,
// Version 2.0 (the "License"); you may not use this file except
// in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.

# Retrieve the employee data.
#
# + workEmail - Employee email
# + return - Employee object or Error if so
public isolated function fetchEmployee(string workEmail) returns Employee|error? {
    string document = string `
        query employeeQuery ($workEmail: String!) {
            employee(email: $workEmail) {
                employeeId
                workEmail
                firstName
                lastName
                jobRole
                employeeThumbnail
            }
        }
    `;

    EmployeeResponse employeeResponse = check hrClient->execute(document, {workEmail});
    Employee? employee = employeeResponse.data.employee;

    return employee;
}

# Retrieve all employees with only email, firstName, and lastName.
#
# + return - Array of EmployeeListItem objects or Error if so
public isolated function fetchAllEmployees() returns EmployeeListItem[]|error {
    string document = string `
        query employeesQuery ($filter: EmployeeFilter, $limit: Int, $offset: Int) {
            employees(filter: $filter, limit: $limit, offset: $offset) {
                workEmail
                firstName
                lastName
            }
        }
    `;

    map<json> variables = {
        "filter": {},
        "limit": MAX_EMPLOYEES_LIMIT,
        "offset": 0
    };

    json response = check hrClient->execute(document, variables);
    EmployeesResponse employeesResponse = check response.cloneWithType(EmployeesResponse);
    EmployeeListItem[] employees = employeesResponse.data.employees;
    
    return employees;
}
